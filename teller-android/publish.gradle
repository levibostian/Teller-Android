// Maven install
apply plugin: 'com.github.dcendents.android-maven'

group = publishedGroupId // Maven Group ID for the artifact
version = libraryVersion

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId publishedGroupId
            artifactId artifact

            name libraryName
            description libraryDescription
            url siteUrl

            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

// some artifacts
apply plugin: 'org.jetbrains.dokka-android'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// Bintray publish
if (!System.getenv('CI')) { // Disable block on CI server as an exception will be thrown in the code below trying to read `local.properties` file as it will not exist on a CI server. If in the future we decide to have a CI server deploy the library, we will load values from environment variables so we can replace the code then. For now, all deployments are happening on a local dev machine with local.properties file populated.
    apply plugin: 'com.jfrog.bintray'

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    bintray {
        user = properties.getProperty("bintray.user")
        key = properties.getProperty("bintray.apikey")

        configurations = ['archives']
        pkg {
            repo = bintrayRepo
            name = bintrayName
            desc = libraryDescription
            websiteUrl = siteUrl
            vcsUrl = gitUrl
            licenses = allLicenses
            publish = true
            override = false // Overwrite published versions already published.
            publicDownloadNumbers = true
            version {
                desc = libraryDescription
            }
        }
    }
}