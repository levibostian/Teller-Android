apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

ext {
    moduleArtifactId = 'teller-android'
}

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            multiDexEnabled false
        }
    }
    sourceSets {
        // Put code into below directory that you want shared between unit and integration tests.
        String sharedTestDir = 'src/sharedTest/java'
        test.java.srcDirs += sharedTestDir
        androidTest.java.srcDirs += sharedTestDir
    }
    lint {
        warningsAsErrors true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // JUnit
    testImplementation 'junit:junit:4.13.2'

    // Android Testing Support Library's runner and rules
    def androidTestLibVersion = "1.4.0"
    androidTestImplementation "androidx.test:runner:$androidTestLibVersion"
    androidTestImplementation "androidx.test:rules:$androidTestLibVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidTestLibVersion"
    androidTestImplementation "androidx.test.ext:truth:$androidTestLibVersion"

    // Truth
    def truthVersion = "1.1.13"
    testImplementation "com.google.truth:truth:$truthVersion"
    androidTestImplementation "com.google.truth:truth:$truthVersion"

    // Kotlin
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    // Mockito
    def mockitoVersion = '4.3.1'
    def mockitoKotlinVersion = '1.6.0'
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockitoKotlinVersion", {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'org.mockito'
    }
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
    androidTestImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockitoKotlinVersion", {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'org.mockito'
    }

    // Espresso UI Testing
    def espressoVersion = '3.4.0'
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
}
repositories {
    mavenCentral()
}

apply from: rootProject.file('gradle/publish.gradle')